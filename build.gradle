apply plugin: 'groovy'

def GROOVY_VERSION = '2.5.8'
def ALEXA_SDK_VERSION = '2.27.1'
def DYNAMODB_VERSION = '1.11.477'
def JUNIT_VERSION = '5.5.2'
def SPOCK_VERSION = '1.2-groovy-2.5'

jar {
    manifest {
        attributes 'Main-Class': 'com.triviaroyale.TriviaRoyaleStreamHandler'
    }

    from {
        sourceSets.main.runtimeClasspath
                .filter { (it.isDirectory() && it.listFiles().length > 0) || it.isFile() }
                .collect { it.isDirectory() ? it : zipTree(it) }
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://repository.mulesoft.org/nexus/content/repositories/public/'
    }
}

task copyNativeDeps(type: Copy) {
    from(configurations.compile + configurations.testCompile) {
        include '*.dll'
        include '*.dylib'
        include '*.so'
    }
    into 'build/libs'
}

test {
    dependsOn copyNativeDeps
    systemProperty 'java.library.path', 'build/libs'
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:$GROOVY_VERSION"
    compile "com.amazon.alexa:ask-sdk:$ALEXA_SDK_VERSION"

    testCompile "org.codehaus.groovy:groovy-all:$GROOVY_VERSION"
    testCompile "com.amazonaws:DynamoDBLocal:$DYNAMODB_VERSION"
    testCompile "org.junit.jupiter:junit-jupiter-api:$JUNIT_VERSION"
    testCompile "org.spockframework:spock-core:$SPOCK_VERSION"
}
